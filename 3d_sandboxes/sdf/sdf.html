<div class="row">
    <div class="col-sm-1"></div>

    <div class="col-sm-11">
        <h3>Signed distance function</h3>

        <ul class="nav nav-tabs" id="myTab" role="tablist">
            <li class="nav-item" role="presentation">
                <button aria-controls="volumetric-tab-pane" aria-selected="true" class="nav-link active" data-bs-target="#volumetric-tab-pane" data-bs-toggle="tab" id="volumetric-tab" role="tab" type="button">Volumetric</button>
            </li>
            <li class="nav-item" role="presentation">
                <button aria-controls="planar-tab-pane" aria-selected="false" class="nav-link" data-bs-target="#planar-tab-pane" data-bs-toggle="tab" id="planar-tab" role="tab" type="button">Planar</button>
            </li>
        </ul>

        <div class="tab-content" id="myTabContent" style="position: relative; width: 600px;">
            <div aria-labelledby="volumetric-tab" class="tab-pane fade show active" id="volumetric-tab-pane" role="tabpanel" tabindex="0">
                <div id='container-volumetric' style="position: relative; width: 600px; height: 600px;">
                    <canvas id="canvas-volumetric" width="600" height="600" style="position: absolute; top: 0; left: 0;"></canvas>
                </div>

                <div style="margin-bottom: 10px;">
                    <button class="btn btn-light btn-sm" type="button" data-bs-toggle="collapse" data-bs-target="#instructions-volumetric" aria-expanded="false" aria-controls="instructions-volumetric">
                        Instructions
                    </button>
                </div>

                <div id="instructions-volumetric" class="collapse instructions">
                    <div class="card card-body">
                        <h5>Control</h5>
                        <p>To rotate the camera, press the left mouse button and drag the mouse.</p>
                    </div>
                </div>
            </div>

            <div aria-labelledby="planar-tab" class="tab-pane fade show" id="planar-tab-pane" role="tabpanel" tabindex="0">
                <div id='container-planar' style="position: relative; width: 600px; height: 600px;">
                    <canvas id="canvas-planar" width="600" height="600" style="position: absolute; top: 0; left: 0;"></canvas>
                </div>

                <div style="margin-bottom: 10px;">
                    <button class="btn btn-light btn-sm" type="button" data-bs-toggle="collapse" data-bs-target="#instructions-planar" aria-expanded="false" aria-controls="instructions-planar">
                        Instructions
                    </button>
                </div>

                <div id="instructions-planar" class="collapse instructions">
                    <div class="card card-body">
                        <h5>Control</h5>
                        <p>The plane can be moved by pressing Shift + the left mouse button and dragging the mouse.</p>
                        <p>To rotate the camera, press the left mouse button and drag the mouse.</p>
                    </div>
                </div>
            </div>
        </div>
    </div>
</div>


<script id="fragment-shader-volumetric" type="shader">
    precision mediump float;

    uniform sampler2D u_Mwb;

    // Constants
    const float PI = 3.14159265;
    const int nbFctVec = nbFct / 4;


    void getCoefficients(float v, out vec4 T[nbFctVec], out int segment) {
        int id = int(floor(v * float(nbSeg)));
        float tt = (v - float(id) / float(nbSeg)) * float(nbSeg);

        // Manage inputs beyond lower bound
        if (id < 0) {
            tt = tt + float(id);
            id = 0;
        }

        // Manage inputs beyond upper bound
        else if (id > nbSeg - 1) {
            tt = tt + float(id - (nbSeg - 1));
            id = nbSeg - 1;
        }

        // Evaluate polynomials
        for (int n = 0; n < nbFctVec; ++n) {
            T[nbFct / 4 + n].x = pow(tt, float(n * 4));
            T[nbFct / 4 + n].y = pow(tt, float(n * 4 + 1));
            T[nbFct / 4 + n].z = pow(tt, float(n * 4 + 2));
            T[nbFct / 4 + n].w = pow(tt, float(n * 4 + 3));
        }

        segment = id;
    }


    float computeDistance(int xSegment, int ySegment, int zSegment, vec4 Tx[nbFctVec], vec4 Ty[nbFctVec], vec4 Tz[nbFctVec])
    {
        float distance = 0.0;

        // kron(kron(Tz, Ty), Tx) * MWb
        for (int i = 0; i < nbFct; ++i)
        {
            vec4 tz = Tz[i / 4];
            float vz = (i % 4 == 0) ? tz.x : (i % 4 == 1) ? tz.y : (i % 4 == 2) ? tz.z : tz.w;

            for (int j = 0; j < nbFct; ++j)
            {
                vec4 ty = Ty[j / 4];
                float v = vz * ((j % 4 == 0) ? ty.x : (j % 4 == 1) ? ty.y : (j % 4 == 2) ? ty.z : ty.w);

                for (int k = 0; k < nbFctVec; ++k)
                {
                    vec4 mwb = texelFetch(u_Mwb, ivec2(i * nbFct * nbFctVec + j * nbFctVec + k, zSegment * nbSeg * nbSeg + ySegment * nbSeg + xSegment), 0);

                    distance += v * Tx[k].x * mwb.x;
                    distance += v * Tx[k].y * mwb.y;
                    distance += v * Tx[k].z * mwb.z;
                    distance += v * Tx[k].w * mwb.w;
                }
            }
        }

        return distance;
    }


    // Rays
    struct ray_t {
        vec3 origin;
        vec3 direction;
    };

    vec3 evaluateRay(ray_t ray, float t)
    {
        return ray.origin + ray.direction * t;
    }


    // Camera
    struct camera_t {
        float fovy;
        vec3 position;
        vec3 target;
        vec3 up;
    };


    // Viewport
    struct viewport_t {
        vec2 size;
        vec3 u;
        vec3 v;
    };

    viewport_t createViewport(camera_t camera)
    {
        viewport_t viewport;

        float aspectRatio = float(iResolution.x) / float(iResolution.y);
        float h = tan(radians(camera.fovy) / 2.0);

        float focalLength = length(camera.position - camera.target);

        viewport.size.y = 2.0 * h * focalLength;
        viewport.size.x = viewport.size.y * aspectRatio;

        vec3 w = normalize(camera.position - camera.target);
        vec3 u = normalize(cross(camera.up, w));
        vec3 v = cross(w, u);

        // Calculate the vectors across the horizontal and down the vertical viewport edges
        viewport.u = viewport.size.x * u;
        viewport.v = viewport.size.y * v;

        return viewport;
    }


    // Represents the distance of a point to the nearest object, with the color of said object
    struct map_result_t
    {
        float distance;
        vec3 color;
    };

    // Returns the distance of a point to the nearest object, with the color of said object
    map_result_t map(vec3 position)
    {
        vec4 Tx[nbFctVec];
        vec4 Ty[nbFctVec];
        vec4 Tz[nbFctVec];
        int xSegment;
        int ySegment;
        int zSegment;

        getCoefficients(position.x, Tx, xSegment);
        getCoefficients(position.y, Ty, ySegment);
        getCoefficients(position.z, Tz, zSegment);

        float dist = computeDistance(xSegment, ySegment, zSegment, Tx, Ty, Tz);

        return map_result_t(dist, vec3(0.5, 0.0, 0.0));
    }


    // Computes the intersection between a ray and a axis-aligned box, returns the (intersection, exit) distances
    // https://iquilezles.org/articles/boxfunctions
    vec2 intersectBox(ray_t ray, vec3 boxPosition, vec3 boxSize) 
    {
        vec3 m = 1.0 / (ray.direction - 1e-6);
        vec3 n = m * (ray.origin - boxPosition);
        vec3 k = abs(m) * boxSize;
        vec3 t1 = -n - k;
        vec3 t2 = -n + k;
        return vec2( max( max( t1.x, t1.y ), t1.z ),
                     min( min( t2.x, t2.y ), t2.z ) );
    }


    // Represents the result of a raycast in the scene: did it hit an object, at which distance
    // (from the origin of the ray) and the color of the object
    struct raycast_result_t
    {
        bool hit;
        float distance;
        vec3 color;
    };

    // Performs raymarching through the scene, using the given ray
    raycast_result_t raycast(ray_t ray, float offset)
    {
        raycast_result_t result = raycast_result_t(false, 0.0, vec3(0.0, 0.0, 0.0));

        float tmin = 0.1;
        float tmax = 4.0;

        vec2 distances = intersectBox(ray, vec3(0.5, 0.5, 0.5), vec3(0.5, 0.5, 0.5));
        if ((distances.x < distances.y) && (distances.y > 0.0) && (distances.x < tmax))
        {
            tmin = max(distances.x, tmin);
            tmax = min(distances.y, tmax);

            float t = tmin;
            for (int i = 0; (i < 70) && (t < tmax); ++i)
            {
                map_result_t h = map(evaluateRay(ray, t));
                if (abs(h.distance) < (0.0001 * t) + offset)
                {
                    result.hit = true;
                    result.distance = t;
                    result.color = h.color;
                    break;
                }

                t += h.distance;
            }
        }

        return result;
    }


    // Compute the normal at a given position
    // See https://iquilezles.org/articles/normalsSDF
    vec3 computeNormal(vec3 position)
    {
        #define ZERO (min(int(iFrame), 0)) // non-constant zero, to prevent the compiler to inline the map() function 4 times

        vec3 n = vec3(0.0);
        for (int i = ZERO; i < 4; ++i)
        {
            vec3 e = 0.5773 * (2.0 * vec3((((i+3)>>1)&1), ((i>>1)&1), (i&1)) - 1.0);
            n += e * map(position + 0.0005 * e).distance;
        }

        return normalize(n);
    }


    // Compute the fragment color for a given ray
    vec3 render(ray_t ray, out bool hit)
    { 
        // Background color
        vec3 color = vec3(0.5, 0.7, 1.0) + max(-ray.direction.y, 0.0) * 0.3;

        // Raymarching into the scene
        raycast_result_t result = raycast(ray, 0.0);
        if (result.hit)
        {
            vec3 position = evaluateRay(ray, result.distance);
            vec3 normal = computeNormal(position);

            color = vec3(0.0);

            // Ambient
            {
                color += 0.2 * result.color;
            }

            // Sun
            {
                vec3 lightDirection = normalize(vec3(-0.5, 0.4, 0.6));
                vec3 halfDirection = normalize(lightDirection - ray.direction);

                float diffuse = clamp(dot(normal, lightDirection), 0.0, 1.0);
                float specular = pow(clamp(dot(normal, halfDirection), 0.0, 1.0), 8.0);
                specular *= diffuse;
                specular *= 0.04 + 0.96 * pow(clamp(1.0 -dot(halfDirection, lightDirection), 0.0, 1.0), 1.0);
                color += result.color * 2.2 * diffuse * vec3(1.0, 1.0, 1.0);
                color += 5.0 * specular * vec3(1.0, 1.0, 1.0);
            }

            // Backlight
            {
                vec3 lightDirection = normalize(vec3(0.5, 0.0, -0.6));
                float diffuse = clamp(dot(normal, lightDirection), 0.0, 1.0);
                color += result.color * 2.2 * diffuse * vec3(0.5, 0.5, 0.5);
            }

            hit = true;
        }

        return vec3(clamp(color, 0.0, 1.0));
    }

    vec3 render_wave(ray_t ray, float distance, vec3 color, float t)
    { 
        // Raymarching into the scene
        raycast_result_t result = raycast(ray, distance);
        if (result.hit)
        {
            vec3 position = evaluateRay(ray, result.distance);
            color = (0.1 - t * 0.1) * result.color + (0.9 + t * 0.1) * color;
        }

        return vec3(clamp(color, 0.0, 1.0));
    }


    void mainImage(out vec4 fragColor, in vec2 fragCoord)
    {
        // Setup the camera
        vec2 mouse = iMouse.xy / iResolution;

        camera_t camera = camera_t(
            90.0,
            vec3(0.5 + 0.5 * sin(4.0 * PI * mouse.x), 0.5, 0.5 + 0.5 * cos(4.0 * PI * mouse.x)),
            vec3(0.5, 0.5, 0.5),
            vec3(0.0, 1.0, 0.0)
        );

        // Setup the viewport
        viewport_t viewport = createViewport(camera);

        // Calculate the horizontal and vertical delta vectors from pixel to pixel
        vec3 dU = viewport.u / float(iResolution.x);
        vec3 dV = viewport.v / float(iResolution.y);

        // Calculate the location of the lower left pixel, which is at gl_FragCoord = (0, 0)
        vec3 viewportLowerLeft = camera.target - viewport.u / 2.0 - viewport.v / 2.0;
        vec3 pixel00 = viewportLowerLeft + 0.5 * (dU + dV);

        // Create the ray
        vec3 pixelCenter = pixel00 + (gl_FragCoord.x * dU) + (gl_FragCoord.y * dV);
        ray_t ray = ray_t(camera.position, normalize(pixelCenter - camera.position));

        // Rendering
        bool hit;
        vec3 color = render(ray, hit);

        if (!hit)
        {
            float t = iTime * 1.0;
            t = t - floor(t);

            color = render_wave(ray, 0.025 * t, color, 0.0);
            color = render_wave(ray, 0.025 + 0.025 * t, color, 0.0);
            color = render_wave(ray, 0.05 + 0.025 * t, color, 0.0);
            color = render_wave(ray, 0.075 + 0.025 * t, color, 0.0);
            color = render_wave(ray, 0.1 + 0.025 * t, color, 0.0);
            color = render_wave(ray, 0.125 + 0.025 * t, color, t);
        }

        fragColor = vec4(color, 1.0);
    }
</script>


<script id="fragment-shader-planar" type="shader">
    precision mediump float;

    uniform sampler2D u_Mwb;
    uniform float u_planeDistance;

    // Constants
    const float PI = 3.14159265;
    const int nbFctVec = nbFct / 4;


    void getCoefficients(float v, out vec4 T[nbFctVec], out int segment) {
        int id = int(floor(v * float(nbSeg)));
        float tt = (v - float(id) / float(nbSeg)) * float(nbSeg);

        // Manage inputs beyond lower bound
        if (id < 0) {
            tt = tt + float(id);
            id = 0;
        }

        // Manage inputs beyond upper bound
        else if (id > nbSeg - 1) {
            tt = tt + float(id - (nbSeg - 1));
            id = nbSeg - 1;
        }

        // Evaluate polynomials
        for (int n = 0; n < nbFctVec; ++n) {
            T[nbFct / 4 + n].x = pow(tt, float(n * 4));
            T[nbFct / 4 + n].y = pow(tt, float(n * 4 + 1));
            T[nbFct / 4 + n].z = pow(tt, float(n * 4 + 2));
            T[nbFct / 4 + n].w = pow(tt, float(n * 4 + 3));
        }

        segment = id;
    }


    float computeDistance(int xSegment, int ySegment, int zSegment, vec4 Tx[nbFctVec], vec4 Ty[nbFctVec], vec4 Tz[nbFctVec])
    {
        float distance = 0.0;

        // kron(kron(Tz, Ty), Tx) * MWb
        for (int i = 0; i < nbFct; ++i)
        {
            vec4 tz = Tz[i / 4];
            float vz = (i % 4 == 0) ? tz.x : (i % 4 == 1) ? tz.y : (i % 4 == 2) ? tz.z : tz.w;

            for (int j = 0; j < nbFct; ++j)
            {
                vec4 ty = Ty[j / 4];
                float v = vz * ((j % 4 == 0) ? ty.x : (j % 4 == 1) ? ty.y : (j % 4 == 2) ? ty.z : ty.w);

                for (int k = 0; k < nbFctVec; ++k)
                {
                    vec4 mwb = texelFetch(u_Mwb, ivec2(i * nbFct * nbFctVec + j * nbFctVec + k, zSegment * nbSeg * nbSeg + ySegment * nbSeg + xSegment), 0);

                    distance += v * Tx[k].x * mwb.x;
                    distance += v * Tx[k].y * mwb.y;
                    distance += v * Tx[k].z * mwb.z;
                    distance += v * Tx[k].w * mwb.w;
                }
            }
        }

        return distance;
    }


    // Rays
    struct ray_t {
        vec3 origin;
        vec3 direction;
    };

    vec3 evaluateRay(ray_t ray, float t)
    {
        return ray.origin + ray.direction * t;
    }


    // Camera
    struct camera_t {
        float fovy;
        vec3 position;
        vec3 target;
        vec3 up;
    };


    // Viewport
    struct viewport_t {
        vec2 size;
        vec3 u;
        vec3 v;
    };

    viewport_t createViewport(camera_t camera)
    {
        viewport_t viewport;

        float aspectRatio = float(iResolution.x) / float(iResolution.y);
        float h = tan(radians(camera.fovy) / 2.0);

        float focalLength = length(camera.position - camera.target);

        viewport.size.y = 2.0 * h * focalLength;
        viewport.size.x = viewport.size.y * aspectRatio;

        vec3 w = normalize(camera.position - camera.target);
        vec3 u = normalize(cross(camera.up, w));
        vec3 v = cross(w, u);

        // Calculate the vectors across the horizontal and down the vertical viewport edges
        viewport.u = viewport.size.x * u;
        viewport.v = viewport.size.y * v;

        return viewport;
    }


    // Represents the distance of a point to the nearest object, with the color of said object
    struct map_result_t
    {
        float distance;
        vec3 color;
    };

    // Returns the distance of a point to the nearest object, with the color of said object
    map_result_t map(vec3 position)
    {
        vec4 Tx[nbFctVec];
        vec4 Ty[nbFctVec];
        vec4 Tz[nbFctVec];
        int xSegment;
        int ySegment;
        int zSegment;

        getCoefficients(position.x, Tx, xSegment);
        getCoefficients(position.y, Ty, ySegment);
        getCoefficients(position.z, Tz, zSegment);

        float dist = computeDistance(xSegment, ySegment, zSegment, Tx, Ty, Tz);

        return map_result_t(dist, vec3(0.5, 0.0, 0.0));
    }


    // Computes the intersection between a ray and a axis-aligned box, returns the (intersection, exit) distances
    // https://iquilezles.org/articles/boxfunctions
    vec2 intersectBox(ray_t ray, vec3 boxPosition, vec3 boxSize) 
    {
        vec3 m = 1.0 / (ray.direction - 1e-6);
        vec3 n = m * (ray.origin - boxPosition);
        vec3 k = abs(m) * boxSize;
        vec3 t1 = -n - k;
        vec3 t2 = -n + k;
        return vec2( max( max( t1.x, t1.y ), t1.z ),
                     min( min( t2.x, t2.y ), t2.z ) );
    }


    // Represents the result of a raycast in the scene: did it hit an object, at which distance
    // (from the origin of the ray) and the color of the object
    struct raycast_result_t
    {
        bool hit;
        float distance;
        vec3 color;
    };

    // Performs raymarching through the scene, using the given ray
    raycast_result_t raycast(ray_t ray, float offset)
    {
        raycast_result_t result = raycast_result_t(false, 0.0, vec3(0.0, 0.0, 0.0));

        float tmin = 0.1;
        float tmax = 4.0;

        vec2 distances = intersectBox(ray, vec3(0.5, 0.5, 0.5), vec3(0.5, 0.5, 0.5));
        if ((distances.x < distances.y) && (distances.y > 0.0) && (distances.x < tmax))
        {
            tmin = max(distances.x, tmin);
            tmax = min(distances.y, tmax);

            float t = tmin;
            for (int i = 0; (i < 70) && (t < tmax); ++i)
            {
                map_result_t h = map(evaluateRay(ray, t));
                if (abs(h.distance) < (0.0001 * t) + offset)
                {
                    result.hit = true;
                    result.distance = t;
                    result.color = h.color;
                    break;
                }

                t += h.distance;
            }
        }

        return result;
    }


    // Compute the normal at a given position
    // See https://iquilezles.org/articles/normalsSDF
    vec3 computeNormal(vec3 position)
    {
        #define ZERO (min(int(iFrame), 0)) // non-constant zero, to prevent the compiler to inline the map() function 4 times

        vec3 n = vec3(0.0);
        for (int i = ZERO; i < 4; ++i)
        {
            vec3 e = 0.5773 * (2.0 * vec3((((i+3)>>1)&1), ((i>>1)&1), (i&1)) - 1.0);
            n += e * map(position + 0.0005 * e).distance;
        }

        return normalize(n);
    }


    // Compute the fragment color for a given ray
    raycast_result_t render(ray_t ray)
    { 
        // Raymarching into the scene
        raycast_result_t result = raycast(ray, 0.0);
        if (result.hit)
        {
            vec3 position = evaluateRay(ray, result.distance);
            vec3 normal = computeNormal(position);

            vec3 color = vec3(0.0);

            // Ambient
            {
                color += 0.2 * result.color;
            }

            // Sun
            {
                vec3 lightDirection = normalize(vec3(-0.5, 0.4, 0.6));
                vec3 halfDirection = normalize(lightDirection - ray.direction);

                float diffuse = clamp(dot(normal, lightDirection), 0.0, 1.0);
                float specular = pow(clamp(dot(normal, halfDirection), 0.0, 1.0), 8.0);
                specular *= diffuse;
                specular *= 0.04 + 0.96 * pow(clamp(1.0 -dot(halfDirection, lightDirection), 0.0, 1.0), 1.0);
                color += result.color * 2.2 * diffuse * vec3(1.0, 1.0, 1.0);
                color += 5.0 * specular * vec3(1.0, 1.0, 1.0);
            }

            // Backlight
            {
                vec3 lightDirection = normalize(vec3(0.5, 0.0, -0.6));
                float diffuse = clamp(dot(normal, lightDirection), 0.0, 1.0);
                color += result.color * 2.2 * diffuse * vec3(0.5, 0.5, 0.5);
            }

            result.color = clamp(color, 0.0, 1.0);
        }
        else
        {
            // Background color
            result.color = vec3(0.5, 0.7, 1.0) + max(-ray.direction.y, 0.0) * 0.3;
        }

        return result;
    }


    raycast_result_t intersectsPlane(ray_t ray)
    {
        raycast_result_t result = raycast_result_t(false, 0.0, vec3(0.0, 0.0, 0.0));

        vec3 planePos = vec3(0.5, 0.5, u_planeDistance);
        vec3 planeNormal = vec3(0.0, 0.0, (ray.direction.z < 0.0 ? -1.0 : 1.0));

        float denom = dot(planeNormal, ray.direction);
        if(denom > 1e-6)
        {
            vec3 dist = planePos - ray.origin;
            result.distance = dot(dist, planeNormal) / denom;
            result.hit = (result.distance >= 0.0);
        }

        return result;
    }


    void mainImage(out vec4 fragColor, in vec2 fragCoord)
    {
        // Setup the camera
        vec2 mouse = iMouse.xy / iResolution + vec2(0.05, 0.0);

        camera_t camera = camera_t(
            90.0,
            vec3(0.5 + 0.5 * sin(4.0 * PI * mouse.x), 0.5, 0.5 + 0.5 * cos(4.0 * PI * mouse.x)),
            vec3(0.5, 0.5, 0.5),
            vec3(0.0, 1.0, 0.0)
        );

        // Setup the viewport
        viewport_t viewport = createViewport(camera);

        // Calculate the horizontal and vertical delta vectors from pixel to pixel
        vec3 dU = viewport.u / float(iResolution.x);
        vec3 dV = viewport.v / float(iResolution.y);

        // Calculate the location of the lower left pixel, which is at gl_FragCoord = (0, 0)
        vec3 viewportLowerLeft = camera.target - viewport.u / 2.0 - viewport.v / 2.0;
        vec3 pixel00 = viewportLowerLeft + 0.5 * (dU + dV);

        // Create the ray
        vec3 pixelCenter = pixel00 + (gl_FragCoord.x * dU) + (gl_FragCoord.y * dV);
        ray_t ray = ray_t(camera.position, normalize(pixelCenter - camera.position));

        // Rendering
        raycast_result_t objectIntersection = render(ray);
        raycast_result_t planeIntersection = intersectsPlane(ray);

        vec3 color = objectIntersection.color;

        if (!objectIntersection.hit || (planeIntersection.distance < objectIntersection.distance))
        {
            vec3 pos = evaluateRay(ray, planeIntersection.distance);
            if ((pos.x >= 0.0) && (pos.x <= 1.0) && (pos.y >= 0.0) && (pos.y <= 1.0))
            {
                map_result_t h = map(pos);

                vec3 waveColor = vec3(0.9, 0.6, 0.3);
                waveColor *= 1.0 - exp(-500.0 * abs(h.distance));
                waveColor *= 0.8 + 0.2 * cos(500.0 * h.distance - iTime * 10.0);

                float alpha = (clamp(h.distance, 0.125, 0.175) - 0.125) / 0.05;

                color = (h.distance > 0.125) ? (1.0 - alpha) * waveColor + alpha * color : waveColor;
            }
        }

        fragColor = vec4(color, 1.0);
    }
</script>


<py-script>
    from js import document, GlslRenderer, getData
    from pyodide.ffi import to_js
    import numpy as np
    import io

    # Load the data
    file_handler = io.BytesIO(getData("3d_sandboxes/sdf/mwb3D01.npy").to_py())
    data = np.load(file_handler, allow_pickle=True).item()
    Mwb = data['Mwb']

    # Start the volumetric rendering
    renderer_volumetric = GlslRenderer.new(document.querySelector("#canvas-volumetric"))

    renderer_volumetric.setup(
        document.querySelector("#fragment-shader-volumetric").text,
        to_js({
            "nbFct": data["nbFct"],
            "nbSeg": data["nbSeg"],
        })
    );

    renderer_volumetric.setMatrix('u_Mwb', to_js(Mwb.flatten()), Mwb.shape[0] * Mwb.shape[1] * Mwb.shape[2], Mwb.shape[3], True)

    # Start the planar rendering
    renderer_planar = GlslRenderer.new(document.querySelector("#canvas-planar"))

    planeDistance = 0.5
    renderer_planar.setUniform("u_planeDistance", planeDistance);

    renderer_planar.setup(
        document.querySelector("#fragment-shader-planar").text,
        to_js({
            "nbFct": data["nbFct"],
            "nbSeg": data["nbSeg"],
        })
    );

    renderer_planar.setMatrix("u_Mwb", to_js(Mwb.flatten()), Mwb.shape[0] * Mwb.shape[1] * Mwb.shape[2], Mwb.shape[3], True)

    def onPlaneDistanceChanged(evt):
        global planeDistance

        if (evt.buttons == 1) and (evt.button == 0) and evt.shiftKey:
            planeDistance += -evt.movementX * 0.001
            planeDistance = max(0.4, min(planeDistance, 0.728))
            renderer_planar.setUniform("u_planeDistance", planeDistance);

    renderer_planar.canvas.onmousemove = onPlaneDistanceChanged;
</py-script>
